/ Two-sum problem

/ input n, k
input
store n 
input
store k

/ input n integers to array
input_loop,     input
                storei arr_ptr1

                load arr_ptr1
                add one
                store arr_ptr1

                / i += 1
                load i
                add one
                store i

                / if i = n, start solving else keep inputing to array
                subt n 
                skipcond 400
                jump input_loop

/ iniatilisation before start solving
load arr_start
store arr_ptr1 / arr_ptr1 = arr_start

/ i = 0
load zero
store i

outer_loop,     load arr_ptr1
                add one
                store arr_ptr2  / arr_ptr2 = arr_ptr1 + 1

                / j = i + 1
                load i 
                add one
                store j
                
                / if j = n, move to end of outer loop
                / else enter to inner loop
                load j
                subt n 
                skipcond 400
                jns start_inner_loop
                
                / end of outer loop
                load i / i += 1
                add one
                store i

                / arr_ptr1 = &arr[i] + 1
                load arr_ptr1
                add one
                store arr_ptr1

                / if i = n, no answer found.
                / else restart outer loop
                load i
                subt n 
                skipcond 400
                jump outer_loop
/ ending of outer loop => no answer
load zero
output
halt

start_inner_loop, hex 0
inner_loop,     loadi arr_ptr1 
                addi arr_ptr2
                subt k
                / check if arr[i] + arr[j] = k
                skipcond 400
                jump end_inner_loop
                
                / output answer and stop program
                load i
                output
                load j
                output
                halt

                / j += 1
end_inner_loop, load j
                add one
                store j

                / arr_ptr2 = &arr[j] + 1
                load arr_ptr2
                add one
                store arr_ptr2

                / if j = n, go back to outer loop
                / else restart inner loop
                load j
                subt n 
                skipcond 400
                jump inner_loop
                jumpi start_inner_loop



n, dec 0 / array size
k, dec 0 / target
i, dec 0 / loop counter. Initial value must be 0.
j, dec 0 / loop counter
arr_start, hex 100
arr_ptr1, hex 100 / Initial value must be arr_start.
arr_ptr2, hex 0
zero, dec 0
one, dec 1